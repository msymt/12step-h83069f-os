OUTPUT_FORMAT("elf32-h8300")
OUTPUT_ARCH(h8300h)
ENTRY("_start")

MEMORY {
	/* o = 'start address', l = 'length' */
	/* Def: RAM */
	ramall(rwx)	: o = 0xffbf20, l = 0x004000 /* 16KB */
	softvec(rw)	: o = 0xffbf20,	l = 0x000040 /* top of RAM, 64byte */
	ram(rwx)	: o = 0xffc020, l = 0x003f00
	/* スタックを用途別に明示的に分離 */
	userstack(rw)	: o = 0xfff400, l = 0x000000 /* ユーザ・スタック */
	bootstack(rw)	: o = 0xffff00, l = 0x000000 /* ブートスタック */
	intrstack(rw)	: o = 0xffff00, l = 0x000000 /* 割り込みスタック, end of RAM */
}

SECTIONS {
	/*.: location counter*/

	/* initilize with zero, below section start with zero*/
	. = 0x0;

	.softvec : {
		_softvec = . ;
	} > softvec

	/* set all object file's '.text' contents to '.text' section*/
	.text : {
		_text_start = . ;	/* set a symbol pointing to the top of the text section */
		*(.text)
		_etext = . ;		/* Place a symbol pointing to the end of the text section */
	} > ram

	/* Read Only Data, set const defined variable, string literal */
	.rodata : {
		_rodata_start = . ;
		*(.strings)
		*(.rodata)
		*(.rodata.*)
		_erodata = . ;
	} > ram

	.data : {
		_data_start = . ;
		*(.data)
		_edata = . ;
	} > ram

	.bss : {
		_bss_start = . ;
		*(.bss)
		*(COMMON)
		_ebss = . ;
	} > ram

	. = ALIGN(4);	/* 4byte alignment */
	_end = . ;		/* end  of section */

	.userstack : {
		_userstack = .;
	} > userstack
	
	.bootstack : {
		_bootstack = .;
	} > bootstack

	.intrstack : {
		_intrstack = .;
	} > intrstack
}
